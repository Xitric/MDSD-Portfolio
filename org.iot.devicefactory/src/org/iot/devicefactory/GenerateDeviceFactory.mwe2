module org.iot.devicefactory.GenerateDeviceFactory

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.iot.devicefactory"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "utf-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = true
			}
		}
		language = StandardLanguage {
			name = "org.iot.devicefactory.Common"
			fileExtensions = "iot,iotc"
			
			serializer = {
				generateStub = false
			}
			validator = {
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = true
			}
			// Required to distinguish tuples and parenthesized expressions
			// from one another
			parserGenerator = {
				options = {
					backtrack = true
				}
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
		language = StandardLanguage {
			name = "org.iot.devicefactory.DeviceLibrary"
			fileExtensions = "iotc"

			serializer = {
				generateStub = false
			}
			validator = {
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = true
			}
			parserGenerator = {
				options = {
					backtrack = true
				}
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
		language = StandardLanguage {
			name = "org.iot.devicefactory.DeviceFactory"
			fileExtensions = "iot"
			// So that we can refer to names defined in .iotc files
			referencedResource = "platform:/resource/org.iot.devicefactory/src/org/iot/devicefactory/DeviceLibrary.xtext"
			
			serializer = {
				generateStub = false
			}
			validator = {
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = true
			}
			parserGenerator = {
				options = {
					backtrack = true
				}
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
