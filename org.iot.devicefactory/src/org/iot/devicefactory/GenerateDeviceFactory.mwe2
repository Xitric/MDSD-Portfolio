module org.iot.devicefactory.GenerateDeviceFactory

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.iot.devicefactory"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "utf-8"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = true
			}
		}
		language = StandardLanguage {
			name = "org.iot.devicefactory.Common"
			//Common is only intended as an abstract library-language
			fileExtensions = "commonDoNotUseSeriouslyAKittenDiesEveryTimeYouUseThisExtension"
			
			serializer = {
				generateStub = false
			}
			validator = {
				generateDeprecationValidation = true
			}
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			generator = {
				generateStub = false
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
		language = StandardLanguage {
			name = "org.iot.devicefactory.DeviceLibrary"
			fileExtensions = "iotc"

			serializer = {
				generateStub = false
			}
			validator = {
				generateDeprecationValidation = true
			}
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			generator = {
				// Should not generate on its own, but is invoked when used by
				// a deployment
				generateStub = false
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
		language = StandardLanguage {
			name = "org.iot.devicefactory.DeviceFactory"
			fileExtensions = "iot"
			// So that we can refer to names defined in .iotc files
			referencedResource = "platform:/resource/org.iot.devicefactory/src/org/iot/devicefactory/DeviceLibrary.xtext"
			
			serializer = {
				generateStub = false
			}
			validator = {
				generateDeprecationValidation = true
			}
			formatter = {
				generateStub = true
				generateXtendStub = true
			}
			generator = {
				generateXtendStub = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
