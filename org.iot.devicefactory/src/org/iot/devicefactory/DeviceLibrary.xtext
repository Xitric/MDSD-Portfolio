grammar org.iot.devicefactory.DeviceLibrary with org.iot.devicefactory.Common

generate deviceLibrary "http://www.iot.org/devicefactory/DeviceLibrary"

Library:
	('package' name=QualifiedName)?
	boards += Board*
;

Board:
	'define' 'board' name=ID ('includes' parents+=[Board|QualifiedName] (',' parents+=[Board|QualifiedName])*)? BEGIN
		sensors += SensorDefinition+
	END
;

SensorDefinition:
	BaseSensorDefinition | OverrideSensorDefinition
;

// The preprocess is stored in each sensor rule separately, because of an Xtext
// bug:
// https://bugs.eclipse.org/bugs/show_bug.cgi?id=485118
BaseSensorDefinition:
	'sensor' name=ID input=SensorInput (BEGIN
		preprocess = Preprocess
	END)?
;

OverrideSensorDefinition:
	'override' 'sensor' parent=[SensorDefinition|QualifiedName] (BEGIN
		preprocess = Preprocess
	END)?
;

SensorInput:
	I2C | Pin
;

Pin:
	'pin' '(' pins+=INTEGER (',' pins+=INTEGER)* ')' 'as' variables=VariableDeclaration
;

I2C:
	'i2c' '(' address=INTEGER ')' 'as' variables=VariableDeclaration
;

Preprocess:
	'preprocess' pipeline=Pipeline
;
