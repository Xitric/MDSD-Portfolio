grammar org.iot.devicefactory.DeviceLibrary with org.iot.devicefactory.Common

generate deviceLibrary "http://www.iot.org/devicefactory/DeviceLibrary"

Library:
	('package' name=QualifiedName)?
	boards += Board*
;

Board:
	'define' 'board' name=ID ('includes' parent=[Board|QualifiedName])? BEGIN
		sensors += Sensor+
	END
;

Sensor:
	BaseSensor | OverrideSensor
;

BaseSensor:
	'sensor' name=ID input=SensorInput (BEGIN
		preprocess = Preprocess
	END)?
;

OverrideSensor:
	'override' 'sensor' name=ID BEGIN
		preprocess = Preprocess
	END
;

SensorInput:
	I2C | Pin
;

Pin:
	'pin' '(' pins+=INTEGER (',' pins+=INTEGER)* ')' 'as' variables=VariableDeclaration
;

I2C:
	'i2c' '(' address=INTEGER ')' 'as' variables=VariableDeclaration
;

Preprocess:
	'preprocess' pipeline=Pipeline
;
