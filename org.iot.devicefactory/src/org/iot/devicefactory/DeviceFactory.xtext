grammar org.iot.devicefactory.DeviceFactory with org.iot.devicefactory.Common

import "http://www.iot.org/devicefactory/DeviceLibrary" as Library

generate deviceFactory "http://www.iot.org/devicefactory/DeviceFactory"

Deployment:
	// There should be at least one library - otherwise we cannot define any
	// devices
	libraries+=Library*
	language=Language
	
	// We want to be able to specify these things in any order
	// We then use a validator to ensure that the correct number of each has
	// been specified. This would have been difficult to express in the grammar
	// itself
	(channels+=Channel | devices+=Device | fogs+=Fog | clouds+=Cloud)*
;

Library:
	'library' importedNamespace=QualifiedNameWithWildcard
;

Language:
	'language' name=ID
;

Channel:
	'channel' name=ID
;

// Inlined rules due to Xtext bug:
// https://bugs.eclipse.org/bugs/show_bug.cgi?id=485118
Device:
	(
		{BaseDevice} 'device' name=ID 'board' board=[Library::Board|QualifiedName] |
		{ChildDevice} 'device' name=ID 'includes' parent=[Device]
	) BEGIN
		('in' input=[Channel])?
		sensors+=Sensor+
	END
;

// This is how I would have liked to represent it
//BaseDevice:
//	'device' name=ID 'board' board=[Library::Board|QualifiedName]
//;
//
//ChildDevice:
//	'device' name=ID 'includes' parent=[Device]
//;

// Inlined rules due to Xtext bug:
// https://bugs.eclipse.org/bugs/show_bug.cgi?id=485118
Sensor:
	(
		{BaseSensor} 'sensor' definition=[Library::SensorDefinition] sampler=Sampler |
		{OverrideSensor} 'override' 'sensor' parent=[Sensor] (sampler=Sampler)?
	) BEGIN
		datas+=SensorData+
	END
;

// This is how I would have liked to represent it
//BaseSensor:
//	'sensor' definition=[Library::SensorDefinition] sampler=Sampler
//;
//
//OverrideSensor:
//	'override' 'sensor' parent=[Sensor] (sampler=Sampler)?
//;

SensorData:
	'data' name=ID BEGIN
		outputs += SensorOut+
	END
;

SensorOut:
	'out' channel=[Channel] pipeline=Pipeline?
;

Sampler:
	'sample' ({FrequencySampler} 'frequency' delay=INT | {SignalSampler} 'signal')
;

Fog:
	'fog' BEGIN
		transformations+=Transformation+ 
	END
;

Cloud:
	'cloud' BEGIN
		transformations+=Transformation+ 
	END
;

Transformation:
	'transformation' provider=[Data] 'as' variables=VariableDeclaration BEGIN
		datas+=TransformationData+
	END
;

TransformationData:
	'data' name=ID BEGIN
		outputs += TransformationOut+ 	
	END
;

// A transformation would not make much sense without a pipeline
TransformationOut: 
	'out' pipeline=Pipeline
;

Data:
	SensorData | TransformationData
;

Out:
	SensorOut | TransformationOut
;
